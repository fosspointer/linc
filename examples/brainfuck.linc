// Brainfuck interpreter in linc!

#include `std.linc`
tape: mut u8[32768u64]
position: mut u64

fn interpret(input: string) {
    current_char: mut char;
    loop: mut u32 = 0u;

    for i mut := 0u64; i < +input; ++i {
        current_char = input[i];

        match current_char {
            '>' => ++position,
            '<' => --position,
            '+' => ++tape[position],
            '-' => --tape[position],
            '.' => putc(as mut char(tape[position])),
            ',' => tape[position] = as u8 (readc()),
            '[' => {
                if tape[position] == 0u8 {
                    loop = 1u;
                    while loop > 0u {
                        current_char = input[++i];
                        if current_char == '[' {
                            ++loop;
                        } else if current_char == ']' {
                            --loop;
                        };
                    };
                };
            },
            ']' => {
                if tape[position] != 0u8 {
                    loop = 1u;
                    while loop > 0u {
                        current_char = input[--i];
                        if current_char == '[' {
                            --loop;
                        } else if current_char == ']' {
                            ++loop;
                        };
                    };
                };
            }
        };
    };
}

fn main(args: string[]): i32 {
    if +args == 2u64 {
        filename: string = args[1u64];
        str := readFile(filename);
        interpret(str);
        return 0i32;
    }
    else if +args > 2u64 {
        putln("Usage:");
        putln(args[1u64] + ": interpret code from stdin.");
        putln(args[1u64] + "[FILE]: interpret code from a file.");
        return -1i32;
    };

    putln("<Brainfuck interpreter in linc. Input a new-line character to start code evaluation.>");

    str mut := "";
    line mut := readln("");
    while line != "" {
        str += line;
        line = readln("");
    };

    interpret(str);
    0i32
}